config {
  type: "view",
  schema: "outputs",
  name: "v_user_stats",
  tags: ["daily", "reporting"],
  description: "Create a summary table for all users including statistics on questions, answeers and badges received.",
  dependencies: [{
      dataset: "intermediate",
      name: "v_users"
    },
    {
      dataset: "intermediate",
      name: "v_badges"
    },
    {
      dataset: "outputs",
      name: "v_all_posts"
    }
  ],
  columns: {
    user_id: `A unique identifier for a user`,
    age: `The age of a user`,
    creation_date: `The date this user signed up`,
    user_tenure: `The number of years since the user's creation date`,
    badge_count: `The all-time number of badges the user has received`,
    questions_and_answer_count: `The all-time number of questions and answers the user has created`,
    question_count: `The all-time number of questions the user has created`,
    answer_count: `The all-time number of answers the user has created`,
    last_badge_received_at: `The time the user received their most recent badge`,
    last_posted_at: `The time the user last posted a question or answer`,
    last_question_posted_at: `The time the user last posted an answer`,
    last_answer_posted_at: `The time the user last posted a question`,
  },
  assertions: {
    uniqueKey: ["user_id"],
    rowConditions: ["badge_count >= 0"]
  }
}


SELECT
    ${common.createHash("v_users.user_id")}                                          AS hashed_user_id,
    v_users.user_id,
    v_users.age,
    ${common.ageBucket("v_users.age")}                                          AS age_bucket,
    v_users.creation_date,
    v_users.user_tenure,
    COUNT(DISTINCT v_badges.badge_id)                                AS badge_count,
    COUNT(DISTINCT v_all_posts.post_id)                              AS questions_and_answer_count,
    COUNT(DISTINCT IF(type = "question", v_all_posts.post_id, NULL)) AS question_count,
    COUNT(DISTINCT IF(type = "answer", v_all_posts.post_id, NULL))   AS answer_count,
    MAX(v_badges.award_timestamp)                                    AS last_badge_received_at,
    MAX(v_all_posts.created_at)                                      AS last_posted_at,
    MAX(IF(type = "question", v_all_posts.created_at, NULL))         AS last_question_posted_at,
    MAX(IF(type = "answer", v_all_posts.created_at, NULL))           AS last_answer_posted_at
FROM
    ${ref("v_users")} AS v_users
LEFT JOIN ${ref("v_badges")} AS v_badges
    ON v_users.user_id = v_badges.user_id
LEFT JOIN ${ref("v_all_posts")} AS v_all_posts
    ON v_users.user_id = v_all_posts.owner_user_id
GROUP BY
    1, 2, 3, 4, 5
